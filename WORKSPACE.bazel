workspace(name = "barelymusician")

load("@bazel_tools//tools/build_defs/repo:git.bzl", "git_repository", "new_git_repository")

# TODO(#111): Enable these once Android targets are fully supported.
# android_ndk_repository(
#     name = "androidndk",
# )
# register_toolchains("@androidndk//:all")

# android_sdk_repository(
#     name = "androidsdk",
# )

git_repository(
    name = "googletest",
    commit = "e2239ee6043f73722e7aa812a459f54a28552929",
    remote = "https://github.com/google/googletest.git",
    shallow_since = "1623433346 -0700",
)

new_git_repository(
    name = "dr_libs",
    build_file_content = """
load("@rules_cc//cc:defs.bzl", "cc_library")

package(default_visibility = ["//visibility:public"])

licenses(["notice"])

cc_library(
    name = "dr_wav",
    hdrs = ["dr_wav.h"],
)
    """,
    commit = "4aff56541f5e6bd4b74053a0d5c9e6156e736059",
    remote = "https://github.com/mackron/dr_libs.git",
    shallow_since = "1641552114 +1000",
)

new_git_repository(
    name = "midifile",
    build_file_content = """
load("@rules_cc//cc:defs.bzl", "cc_library")

package(default_visibility = ["//visibility:public"])

licenses(["notice"])

exports_files(["LICENSE.txt"])

cc_library(
    name = "midifile",
    srcs = glob(["src/*.cpp"]),
    hdrs = glob(["include/*.h"]),
    copts = select({
        "@platforms//os:windows": ["-w"],
        "//conditions:default": [],
    }),
    includes = ["include"],
)
    """,
    commit = "de6aa0c8f82f9dff29b62dba013a65c9034d633d",
    remote = "https://github.com/craigsapp/midifile.git",
    shallow_since = "1624599610 -0700",
)

new_git_repository(
    name = "portaudio",
    build_file_content = """
load("@rules_cc//cc:defs.bzl", "cc_library")

package(default_visibility = ["//visibility:public"])

licenses(["reciprocal"])

exports_files(["LICENSE.txt"])

cc_library(
    name = "portaudio",
    srcs = glob([
        "src/common/*.c",
        "src/common/*.h",
        "include/*.h",
    ]) + select({
        "@platforms//os:linux": glob([
            "src/hostapi/alsa/*.c",
            "src/hostapi/alsa/*.h",
            "src/os/unix/*.c",
            "src/os/unix/*.h",
        ]),
        "@platforms//os:osx": glob([
            "src/hostapi/coreaudio/*.c",
            "src/hostapi/coreaudio/*.h",
            "src/os/unix/*.c",
            "src/os/unix/*.h",
        ]),
        "@platforms//os:windows": glob([
            "src/hostapi/wmme/*.c",
            "src/hostapi/wmme/*.h",
            "src/os/win/*.c",
            "src/os/win/*.h",
        ]),
        "//conditions:default": [],
    }),
    hdrs = ["include/portaudio.h"],
    copts = ["-w"],
    defines = select({
        "@platforms//os:linux": ["PA_USE_ALSA=1"],
        "@platforms//os:osx": ["PA_USE_COREAUDIO=1"],
        "@platforms//os:windows": ["PA_USE_WMME=1"],
        "//conditions:default": [],
    }),
    includes = [
        "include",
        "src/common",
        "src/os/unix",
    ],
    linkopts = select({
        "@platforms//os:linux": [
            "-lasound",
            "-pthread",
        ],
        "@platforms//os:osx": [
            "-framework AudioUnit",
            "-framework CoreAudio",
            "-framework AudioToolbox",
            "-framework Carbon",
        ],
        "//conditions:default": [],
    }),
)
    """,
    commit = "147dd722548358763a8b649b3e4b41dfffbcfbb6",
    remote = "https://github.com/PortAudio/portaudio.git",
    shallow_since = "1617236250 -0700",
)
