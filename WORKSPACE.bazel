workspace(name = "barelymusician")

load("@bazel_tools//tools/build_defs/repo:git.bzl", "git_repository", "new_git_repository")

git_repository(
    name = "googletest",
    commit = "e7e591764baba0a0c3c9ad0014430e7a27331d16",
    remote = "https://github.com/google/googletest.git",
    shallow_since = "1614877659 -0500",
)

new_git_repository(
    name = "dr_libs",
    build_file_content = """
load("@rules_cc//cc:defs.bzl", "cc_library")

package(default_visibility = ["//visibility:public"])

licenses(["notice"])

cc_library(
    name = "dr_wav",
    hdrs = ["dr_wav.h"],
)
    """,
    commit = "cac1785cee4abb455817b43d5dee33b49d61be2f",
    remote = "https://github.com/mackron/dr_libs.git",
)

new_git_repository(
    name = "midifile",
    build_file_content = """
load("@rules_cc//cc:defs.bzl", "cc_library")

package(default_visibility = ["//visibility:public"])

licenses(["notice"])

exports_files(["LICENSE.txt"])

cc_library(
    name = "midifile",
    srcs = glob(["src/*.cpp"]),
    hdrs = glob(["include/*.h"]),
    copts = select({
        "@platforms//os:windows": ["-w"],
        "//conditions:default": [],
    }),
    includes = ["include"],
)
    """,
    commit = "988d55b80a9b85fc194454293950cf233cdcb8c4",
    remote = "https://github.com/craigsapp/midifile.git",
    shallow_since = "1591146060 -0700",
)

new_git_repository(
    name = "portaudio",
    build_file_content = """
load("@rules_cc//cc:defs.bzl", "cc_library")

package(default_visibility = ["//visibility:public"])

licenses(["reciprocal"])

exports_files(["LICENSE.txt"])

cc_library(
    name = "portaudio",
    srcs = glob([
        "src/common/*.c",
        "src/common/*.h",
        "include/*.h",
    ]) + select({
        "@platforms//os:osx": glob([
            "src/hostapi/coreaudio/*.c",
            "src/hostapi/coreaudio/*.h",
            "src/os/unix/*.c",
            "src/os/unix/*.h",
        ]),
        "@platforms//os:windows": glob([
            "src/hostapi/wmme/*.c",
            "src/hostapi/wmme/*.h",
            "src/os/win/*.c",
            "src/os/win/*.h",
        ]),
        "//conditions:default": [],
    }),
    hdrs = ["include/portaudio.h"],
    copts = select({
        "@platforms//os:osx": ["-Wno-deprecated-declarations"],
        "//conditions:default": [],
    }),
    defines = select({
        "@platforms//os:osx": ["PA_USE_COREAUDIO=1"],
        "@platforms//os:windows": ["PA_USE_WMME=1"],
        "//conditions:default": [],
    }),
    includes = [
        "include",
        "src/common",
        "src/os/unix",
    ],
    linkopts = select({
        "@platforms//os:osx": [
            "-framework AudioUnit",
            "-framework CoreAudio",
            "-framework AudioToolbox",
            "-framework Carbon",
        ],
        "//conditions:default": [],
    }),
)
    """,
    commit = "eec7bb739771381ab698a6a611d933bdd72cbd8f",
    remote = "https://github.com/PortAudio/portaudio.git",
    shallow_since = "1616176291 -0700",
)
